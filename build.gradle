/*
 * Copyright 2021 Aiven Oy https://aiven.io
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    // https://docs.gradle.org/current/userguide/java_library_plugin.html
    id "java-library"

    // https://docs.gradle.org/current/userguide/distribution_plugin.html
    id "distribution"

    // https://docs.gradle.org/current/userguide/checkstyle_plugin.html
    id "checkstyle"

    // https://docs.gradle.org/current/userguide/jacoco_plugin.html
    id "jacoco"

    // https://docs.gradle.org/current/userguide/publishing_maven.html
    id "maven-publish"

    // https://github.com/melix/jmh-gradle-plugin
    id "me.champeau.jmh" version "0.6.2"

    //https://plugins.gradle.org/plugin/com.github.johnrengelman.shadow
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    withSourcesJar()
}

compileJava {
    options.compilerArgs = ["-Xlint:all", "-Werror"]
}

jmh {
    jmhVersion = "1.28"
    resultFormat = "TEXT"
}

dependencies {
    implementation "com.google.guava:guava:30.1-jre"
    implementation "com.google.code.gson:gson:2.8.6"

    compileOnly "org.apache.kafka:kafka_2.12:2.1.1"
    compileOnly "org.slf4j:slf4j-api:1.7.25"

    testImplementation "org.apache.kafka:kafka_2.12:2.1.1"

    testImplementation "org.slf4j:slf4j-log4j12:1.7.25"

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.7.1"
    testImplementation "org.junit.jupiter:junit-jupiter:5.7.1"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.7.1"

    testImplementation "org.mockito:mockito-core:3.8.0"
    testImplementation "org.mockito:mockito-junit-jupiter:3.8.0"

    testImplementation "org.hamcrest:hamcrest-library:2.2"

    testImplementation "org.openjdk.jmh:jmh-core:1.28"
    testImplementation "org.openjdk.jmh:jmh-generator-annprocess:1.28"

    jmh "org.apache.kafka:kafka_2.12:2.1.1"
}

group = "io.aiven"

processResources {
    filesMatching("aiven-kafka-auth-version.properties") {
        expand(version: version)
    }
}

checkstyle {
    toolVersion "8.29"
    getConfigDirectory().set(rootProject.file("checkstyle/"))
}

jacoco {
    toolVersion = "0.8.5"
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes(
                "Version": "${project.version}"
        )
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = getGroup()
            artifactId = "aiven-kafka-auth"
            version = getVersion()
            pom {
                name = "Aiven Kafka authorization"
                description = "Aiven authorization extensions for Kafka"
                url = "https://aiven.io"
                organization {
                    name = "Aiven Oy"
                    url = "https://aiven.io"
                }
                licenses {
                    license {
                        name = "Apache 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0"
                        distribution = "repo"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/aiven/aiven-kafka-auth.git"
                    developerConnection = "scm:git:git@github.com:aiven/aiven-kafka-auth.git"
                    url = "https://github.com/aiven/aiven-kafka-connect-gcs.git"
                    tag = "HEAD"
                }
            }
            from components.java
        }
    }
}
